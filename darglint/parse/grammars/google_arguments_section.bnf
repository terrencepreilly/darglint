import google_base.bnf
import google_types.bnf
import noqa_statement.bnf

from darglint.errors import (
    EmptyDescriptionError,
    EmptyTypeError,
    IndentError,
)
from darglint.parse.identifiers import (
    ArgumentIdentifier,
    ArgumentItemIdentifier,
    ArgumentTypeIdentifier,
)

Grammar: ArgumentsGrammar

start: <arguments-section>

<arguments-section>
    ::= <arguments> <colon> <newline> <items-argument> <newlines>

<items-argument>
    ::= <item-argument> <newline> <items-argument>
    | <item-argument>

<item-argument>
    ::= @ArgumentItemIdentifier
        <head-argument> <item-body>
    | @EmptyDescriptionError <head-argument>

<head-argument>
    ::= @ArgumentIdentifier
      <indent> <ident> <colon>
    | 2 @ArgumentIdentifier
      @EmptyTypeError
      <indent> <ident> <lparen> <rparen> <colon>
    | @ArgumentIdentifier
      @ArgumentTypeIdentifier
      <indent> <ident> <type-section-parens> <colon>
    | @ArgumentIdentifier
      @ArgumentTypeIdentifier
      <indent> <ident> <type-section-parens> <colon> <newline>
    | @ArgumentIdentifier
      <indent> <ident> <colon> <newline>

# The normal <paragraph> can be indented any number of times,
# but <paragraph-indented-two> has at least two indents.  By
# weighing it more in the grammar, we capture under-indented
# lines.
#
# We also need to have a version with a newline and one without
# a newline, so that we can handle the last line in the arguments
# section as well as all other lines.
<item-body>
    ::= 2 <line> <newline> <paragraph-indented-two>
    | 2 <line> <newline> <paragraph-indented-two> <newline>
    | 2 <line> <newline>
    | 2 <line>
    | @IndentError
      <line> <newline> <paragraph>
    | @IndentError
      <line> <newline> <paragraph> <newline>
